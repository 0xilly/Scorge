buildscript {
    repositories {
        maven { url 'https://maven.minecraftforge.net/' }
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1.+', changing: true
    }
}

plugins {
    id 'org.ajoberstar.grgit' version '4.1.1'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
    id 'maven-publish'
    id 'scala'
}

apply plugin: 'net.minecraftforge.gradle'
version = getVersion()
group = 'net.minecraftforge'
archivesBaseName = 'Scorge'

repositories {
    mavenCentral()
    flatDir {
        dirs 'build/libs'
    }
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

sourceSets {
    example {}
}

minecraft {
    // the mappings can be changed at any time, and must be in the following format.
    // snapshot_YYYYMMDD   snapshot are built nightly.
    // stable_#            stables are built at the discretion of the MCP team.
    // Use non-default mappings at your own risk. they may not always work.
    // simply re-run your setup task after changing the mappings to update your workspace.
    mappings channel: 'official', version: "${mappings_version}"

    runs {
        client = {
            // recommended logging data for a userdev environment
            properties 'forge.logging.markers': 'REGISTRIES'
            // recommended logging level for the console
            properties 'forge.logging.console.level': 'debug'
            workingDirectory project.file('run').canonicalPath

            mods {
                scorge {
                    source sourceSets.main
                }
                scorgetest {
                    source sourceSets.example
                }
            }
        }
        server = {
            // recommended logging data for a userdev environment
            properties 'forge.logging.markers': 'REGISTRIES'
            // recommended logging level for the console
            properties 'forge.logging.console.level': 'debug'
            workingDirectory project.file('run').canonicalPath

            mods {
                scorge {
                    source sourceSets.main
                }
                scorgetest {
                    source sourceSets.example
                }
            }
        }
    }
}

configurations {
    library
    implementation.extendsFrom library
    exampleImplementation.extendsFrom implementation
}

minecraft.runs.all {
    lazyToken('minecraft_classpath') {
        configurations.library.copyRecursive().resolve().collect { it.absolutePath }.join(File.pathSeparator)
    }
}

dependencies {
    minecraft "net.minecraftforge:forge:${mc_version}-${forge_version}"
    library "org.scala-lang:scala3-library_3:${scala_lib}"
}

jar {
    manifest {
        attributes([
                'FMLModType': 'LANGPROVIDER'
        ])

        attributes(['Specification-Title': 'Mod Language Provider',
                    'Specification-Vendor': 'Forge',
                    'Specification-Version': '1.0',
                    'Implementation-Title': 'Scorge',
                    'Implementation-Version': getVersion(),
                    'Implementation-Vendor' :'Scorge',
                    'Implementation-Timestamp': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                    'Git-Commit': grgit.head().abbreviatedId,
                    'Git-Branch': grgit.branch.current().getName() ],
                'net/minecraftforge/scorge/lang/')
    }
}

shadowJar {
    classifier = ''
    dependencies {
        include(dependency("org.scala-lang:scala3-library_3:${scala_lib}"))
    }
}

tasks.build.dependsOn shadowJar

artifacts {
    archives shadowJar
}

publishing {
    publications {
        shadow(MavenPublication) { publication ->
            project.shadow.component(publication)

            pom {
                name = 'Scorge'
                description = 'The offical Scala language loader for Forge'
                url = 'https://github.com/MinecraftForge/Scorge/'
                scm {
                    url = 'https://github.com/MinecraftForge/Scorge'
                    connection = 'scm:git:git://github.com/MinecraftForge/scorge.git'
                    developerConnection = 'scm:git:git@github.com:MinecraftForge/scorge.git'
                }
                issueManagement {
                    system = 'github'
                    url = 'https://github.com/MinecraftForge/Scorge/issues'
                }

                licenses {
                    license {
                        name = 'LGPLv2.1'
                        url = 'https://www.gnu.org/licenses/old-licenses/lgpl-2.1.txt'
                    }
                }

                developers {
                    developer {
                        id = 'illyohs'
                        name = 'Anthony Anderson'
                    }
                }
            }
        }
    }
    repositories {
        maven {
            credentials {
                username project.properties.forgeMavenUser?:'fake'
                password project.properties.forgeMavenPassword?:'news'
            }
            url 'http://maven.minecraftforge.net/manage/upload'
        }
    }
}

def getVersion()
{
    String base_number = "${mod_version}"
    if (System.hasProperty("BUILD_NUMBER").toString())
    {
        return base_number + "." + System.getenv("BUILD_NUMBER").toString()
    } else {
        return base_number + ".${grgit.head().abbreviatedId}"
    }
}